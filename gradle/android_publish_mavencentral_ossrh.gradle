if (versions == null || versions.publish_version == null) {
  throw new IllegalStateException("Unable to reference publish_version!")
} else if (module_group == null || module_name == null) {
  throw new IllegalStateException("Must provide module_group and module_name!")
}

group = module_group
version = versions.publish_version

apply plugin: 'maven-publish'
apply plugin: 'signing'

signing {
  useGpgCmd() //ä½¿ç”¨ gpg-agent
  sign publishing.publications
}

task androidSourcesJar(type: Jar) {
  archiveClassifier.set('sources')
  if (project.plugins.findPlugin("com.android.library")) {
    // For Android libraries
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
  } else {
    // For pure Kotlin libraries, in case you have them
    from sourceSets.main.java.srcDirs
    from sourceSets.main.kotlin.srcDirs
  }
}

artifacts {
  archives androidSourcesJar
}

// ç›¸å…³ç§å¯†ä¿¡æ¯ä¸å¯ç›´æ¥å®šä¹‰åœ¨é¡¹ç›®ä¸­ï¼Œæœ€å¥½å®šä¹‰åœ¨ç”¨æˆ·çš„ Gradle ä¸»ç›®å½•çš„ gradle.properties æ–‡ä»¶æˆ–è€…ç¯å¢ƒå˜é‡ä¸­
ext["signing.keyId"] = hasProperty('signing.keyId') ? property('signing.keyId') : System.getenv('SIGNING_KEY_ID')
ext["signing.password"] = hasProperty('signing.password') ? property('signing.password') : System.getenv('SIGNING_PASSWORD')
ext["signing.secretKeyRingFile"] = hasProperty('signing.secretKeyRingFile') ? property('signing.secretKeyRingFile') : System.getenv('SIGNING_SECRET_KEY_RING_FILE')
ext["ossrhUsername"] = hasProperty('ossrhUsername') ? property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
ext["ossrhPassword"] = hasProperty('ossrhPassword') ? property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
ext["sonatypeStagingProfileId"] = hasProperty('sonatypeStagingProfileId') ? property('sonatypeStagingProfileId') : System.getenv('SONATYPE_STAGING_PROFILE_ID')

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
  Properties p = new Properties()
  new FileInputStream(secretPropsFile).withCloseable { is -> p.load(is) }
  p.each { name, value -> ext[name] = value }
}

publishing {
  publications {
    release(MavenPublication) {
      // The coordinates of the library, being set from variables that
      // we'll set up later
      groupId module_group
      artifactId module_name
      version versions.publish_version

      // Two artifacts, the `aar` (or `jar`) and the sources
      if (project.plugins.findPlugin("com.android.library")) {
        artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
      } else {
        artifact("$buildDir/libs/${project.getName()}-${version}.jar")
      }
      artifact androidSourcesJar

      // Mostly self-explanatory metadata
      pom {
        packaging 'aar'
        name = module_name
        description = 'ğŸ˜ A beautiful, fluid, and extensible dialogs API for Kotlin & Android.'
        url = 'https://github.com/Heart-Beats/material-dialogs'
        licenses {
          license {
            name = 'Apache 2.0 License'
            url = 'https://github.com/Heart-Beats/material-dialogs/blob/main/LICENSE.md'
          }
        }
        developers {
          developer {
            id = 'afollestad'
            name = 'Aidan Follestad'
            email = 'dont-email-me@af.codes'
          }
          // Add all other devs here...
        }
        // Version control info - if you're using GitHub, follow the format as seen here
        scm {
          connection = 'scm:git:github.com/afollestad/material-dialogs.git'
          developerConnection = 'scm:git:ssh://github.com/afollestad/material-dialogs.git'
          url = 'https://github.com/Heart-Beats/material-dialogs/tree/main'
        }

        //    ä¸€ä¸ªç¨å¾®æœ‰ç‚¹ hacky çš„ä¿®å¤ï¼Œä»¥ä¾¿æ‚¨çš„ POM å°†åŒ…å«æ‚¨çš„åº“æ‰€æ„å»ºçš„ä»»ä½•ä¼ é€’ä¾èµ–é¡¹
        withXml {
          def dependenciesNode = asNode().appendNode('dependencies')

          // è¯¥ä¾èµ–åŒ…æ‹¬ implementation å’Œ api ä¾èµ–
          def implementationDependencies = project.configurations.implementation.allDependencies
          def apiDependencies = project.configurations.api.allDependencies
          def compileOnlyDependencies = project.configurations.compileOnly.allDependencies

          def realImplementationDependencies = implementationDependencies - apiDependencies

          // ç”Ÿæˆ implementation èŠ‚ç‚¹ä¾èµ–
          realImplementationDependencies.each {
            createDependencyNode(dependenciesNode, 'runtime', it.group, it.name, it.version)
          }

          // ç”Ÿæˆ api èŠ‚ç‚¹ä¾èµ–
          apiDependencies.each {
            createDependencyNode(dependenciesNode, 'compile', it.group, it.name, it.version)
          }

          // ç”Ÿæˆ compileOnly èŠ‚ç‚¹ä¾èµ–
          compileOnlyDependencies.each {
            createDependencyNode(dependenciesNode, 'provided', it.group, it.name, it.version)
          }
        }
      }
    }
  }

  // The repository to publish to, Sonatype/MavenCentral
  repositories {
    maven {
      // This is an arbitrary name, you may also use "mavencentral" or
      // any other name that's descriptive for you
      name = "sonatype"
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username ossrhUsername
        password ossrhPassword
      }
    }
  }
}

/**
 *
 * è®¾ç½® pom æ–‡ä»¶çš„ dependenciesèŠ‚ç‚¹
 *      åˆ«çš„é¡¹ç›®ä¾èµ–è¯¥ pom  æ–‡ä»¶æ—¶ï¼Œä¼šä» dependenciesèŠ‚ç‚¹ä¸‹æ ¹æ® scope æ¥é€‰æ‹©æ˜¯å¦ä¸‹è½½ä¾èµ–åº“
 *
 *  scope æœ‰å¦‚ä¸‹å–å€¼ï¼š
 *      compileï¼š      å¦‚æœæ²¡æœ‰æŒ‡å®š scope æ ‡ç­¾ï¼Œmaven é»˜è®¤ä¸ºè¿™ä¸ªèŒƒå›´ã€‚ç¼–è¯‘ä¾èµ–å…³ç³»åœ¨æ‰€æœ‰ classpath ä¸­éƒ½å¯ç”¨ã€‚æ­¤å¤–ï¼Œè¿™äº›ä¾èµ–å…³ç³»è¢«ä¼ æ’­åˆ°ä¾èµ–é¡¹ç›®
 *      providedï¼š    ä¸ compile ç±»ä¼¼ï¼Œä½†æ˜¯è¡¨ç¤ºæ‚¨å¸Œæœ› jdk æˆ–å®¹å™¨åœ¨è¿è¡Œæ—¶æä¾›å®ƒ(å³ç³»ç»Ÿç±»åº“)ã€‚å®ƒåªé€‚ç”¨äºç¼–è¯‘å’Œæµ‹è¯• classpathï¼Œä¸å¯ä¼ é€’
 *      runtimeï¼š    æ­¤èŒƒå›´è¡¨ç¤ºç¼–è¯‘ä¸éœ€è¦ä¾èµ–å…³ç³»ï¼Œè€Œæ˜¯ç”¨äºæ‰§è¡Œã€‚å®ƒæ˜¯åœ¨è¿è¡Œæ—¶å’Œæµ‹è¯• classpathï¼Œä½†ä¸æ˜¯ç¼–è¯‘ classpath
 *      testï¼š      æ­¤èŒƒå›´è¡¨ç¤ºæ­£å¸¸ä½¿ç”¨åº”ç”¨ç¨‹åºä¸éœ€è¦ä¾èµ–å…³ç³»ï¼Œä»…é€‚ç”¨äºæµ‹è¯•ç¼–è¯‘å’Œæ‰§è¡Œé˜¶æ®µã€‚å®ƒä¸æ˜¯ä¼ é€’çš„ã€‚
 *      systemï¼š   æ­¤èŒƒå›´ä¸ provided ç±»ä¼¼ï¼Œé™¤äº†æ‚¨å¿…é¡»æä¾›æ˜ç¡®åŒ…å«å®ƒçš„ jarã€‚è¯¥ artifact å§‹ç»ˆå¯ç”¨ï¼Œå¹¶ä¸”ä¸æ˜¯åœ¨ä»“åº“ä¸­æŸ¥æ‰¾ã€‚
 *
 *      å¸¸ç”¨ç±»æ¯”æ€»ç»“ï¼š
 *
 *       maven ä¸­     |    gradle ä¸­
 *       ---------------------------
 *        compile    |       api
 *       provided    |   compileOnly
 *       runtime     | implementation
 *
 */
private static def createDependencyNode(root, scope, groupId, artifactId, version) {
  def dependencyNode = root.appendNode('dependency')
  dependencyNode.appendNode('groupId', groupId)
  dependencyNode.appendNode('artifactId', artifactId)
  dependencyNode.appendNode('version', version)
  dependencyNode.appendNode('scope', scope)
}

afterEvaluate {
  publishReleasePublicationToSonatypeRepository.dependsOn assembleRelease
}
